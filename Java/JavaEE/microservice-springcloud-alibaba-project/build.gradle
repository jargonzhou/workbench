buildscript {
    ext {
        springBootVersion = '2.3.12.RELEASE'
    }

    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        maven {
            url "https://repo.spring.io/plugins-release/"
        }
        mavenCentral()
    }

    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath 'io.spring.gradle:dependency-management-plugin:1.0.11.RELEASE'
        classpath('io.franzbecker:gradle-lombok:1.14')
    }
}

allprojects {
    group = 'com.spike'
    version = '0.0.1-SNAPSHOT'
}

subprojects {
    apply plugin: "io.spring.dependency-management"
    apply plugin: "org.springframework.boot"
    apply plugin: 'java-library'
    apply plugin: "io.franzbecker.gradle-lombok"

    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
    [compileJava,compileTestJava,javadoc]*.options*.encoding = 'UTF-8'

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    repositories {
        mavenCentral()
    }

    ext {
        set('springCloudAlibabaVersion', "2.2.10-RC1")
        set('springCloudVersion', "Hoxton.SR12")
        set('pagehelper.version', "5.1.6")
        set('pagehelper-boot.version', "1.2.7")
    }

    dependencyManagement {
        imports {
            mavenBom "com.alibaba.cloud:spring-cloud-alibaba-dependencies:${springCloudAlibabaVersion}"
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        }

        dependencies {
            dependency "org.springdoc:springdoc-openapi-ui:1.3.4"
            dependency 'io.swagger.core.v3:swagger-annotations:2.1.2'
            dependency 'org.mybatis.generator:mybatis-generator-core:1.4.0'
            dependency 'mysql:mysql-connector-java:8.0.19'
            dependency "com.github.pagehelper:pagehelper:${project.'pagehelper.version'}"
            dependency "com.github.pagehelper:pagehelper-spring-boot-starter:${project.'pagehelper-boot.version'}"
        }
    }

    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter-web'
        implementation 'org.springframework.cloud:spring-cloud-starter-loadbalancer'
        implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'
        compileOnly 'org.projectlombok:lombok:1.18.8'
        annotationProcessor 'org.projectlombok:lombok:1.18.8'
        testImplementation('org.springframework.boot:spring-boot-starter-test') {
            exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
        }
        testImplementation("junit:junit:4.12")
    }

    tasks.named('test') {
        useJUnitPlatform()
    }
}